# 第29章 空间计量经济学

## 29.1 地理学第一定律
+ 所有事物都与其它事物相关联，但是较近的事物比较远的事物更关联。

## 29.2 空间权重矩阵
+ 地理距离、社交距离、经济距离

## 29.3 空间自相关
### 莫兰指数 $I$：
+ 1、介于(-1，1)之间，大于0表示正自相关，高值与高值相邻，低值与低值相邻，小于0表示负自相关，高值与低值相邻；
+ 2、莫兰散点图，Moran csatterplot 莫兰指数就是指该散点图的斜率。
+ 3、标准化的莫兰指数服从渐近标准正态分布
+ 4、两种莫兰指数：
	+ 全局莫兰指数(Global Moran's I)：考察的是整个空间的序列空间集聚情况。
	+ 局部莫兰指数(Local Moran's I)：考察的是局部区域 $i$ 附近的空间集聚情况。

### 吉尔里指数$C$：
+ 1、(Geary's C)(Geary,1954),也称为吉尔里相邻比率Geary's Contiguity Ratiao:
+ 2、介于（0，2）之间，大于1表示负相关，等于1不相关，小于1正相关；与莫兰指数成反向变动，比莫兰指数局部空间自相关更敏感，但是二者均无法区别热点和冷点；
+ 3、标准化的吉尔里指数服从渐近标准正态分布。

### Getis-Ord指数$G$：
+ 1、Getis and ord（1992），样本中高值聚集在一起，G较大；样本中低值聚集在一起，G较小；
+ 2、G>E(G)存在热点区域；反之存在冷点区域，标准化的G也服从标准正太分布；
+ 3、G>1.96拒绝无空间自相关的原假设，认为存在空间正自相关，且存在热点区域，G<-1.96拒绝无空间自相关的原假设，认为存在空间正自相，且存在冷点区域，
+ 4、局部Getis-Ord指数G，可以考察局部区域i是否为热点或冷点

+ 安装空间自相关命令
```stata
	net install sg162.pkg //下载以spat开头的一系列命令
	//spatwmat 定义空间权重矩阵
	//spatgsa 全局空间自相关检验
	//spatlsa 局部空间自相关检验
	//spatcorr 考察空间自相关指标对距离临界值d的依赖性，小于d取1，大于d取0
	//spatdiag 针对OLS回归结果，诊断是否存在空间效应
	//spatreg 估计空间滞后和空间误差模型
```
+ 空间案例：
```stata
	cd e:\data
	use columbusswm.dta,clear //空间权重矩阵
	br

	use columbusdata.dta,clear //犯罪数据
	br

	spatwmat using columbusswm.dta,name(W) //生成空间权重矩阵W
	matrix list W
	use columbusdata.dta,clear
	spatgsa crime,weights(W) moran geary go twotail //计算三个指数，并进行双边检验，结果均拒绝无空间自相关的原假设，认为存在空间自相关,twotail表示双尾检验；
	spatlsa crime,w(W) moran twotail 			 	//计算局部莫兰指数，很多p值很小的区域，拒绝无空间自相关的原假设，认为存在空间自相关；
```
+ 结果：
```
Measures of global spatial autocorrelation


Weights matrix
--------------------------------------------------------------
Name: W
Type: Imported (binary)
Row-standardized: No
--------------------------------------------------------------

Moran's I
--------------------------------------------------------------
          Variables |    I      E(I)   sd(I)     z    p-value*
--------------------+-----------------------------------------
              crime |  0.521  -0.021   0.087   6.212   0.000
--------------------------------------------------------------

Geary's c
--------------------------------------------------------------
          Variables |    c      E(c)   sd(c)     z    p-value*
--------------------+-----------------------------------------
              crime |  0.584   1.000   0.109  -3.835   0.000
--------------------------------------------------------------

Getis & Ord's G
--------------------------------------------------------------
          Variables |    G      E(G)   sd(G)     z    p-value*
--------------------+-----------------------------------------
              crime |  0.126   0.099   0.006   4.714   0.000 //三个空间全局自相关指标均强烈拒绝“无空间自相关”的原假设，认为存在空间自相关；
--------------------------------------------------------------
*2-tail test
```

## 29.4 空间自回归模型(SAR)
+ 1、Pure SAR：$y=\lambda W y+\varepsilon$ ，纯空间自回归模型。
+ 2、一般线性回归模型：$y = λWy+Xβ+ε$，原假设$λ=0$，来检验是否存在空间效应。
+ 3、空间计量模型的真谛：一个区域的解释变量会对任意区域的被解释变量可能产生影响。
	+	直接效应：区域的变量对本区域被解释变量的效应；
	+	平均直接效应：主对角线上元素进行平均
	+	平均总效应：所有元素平均
	+	平均间接效应=平均总效应-平均直接效应

## 29.5 空间杜宾模型(SDM Spatial Durbin Model)
+ $y = Xβ +WXδ + ε$：
	+ $WXδ$表示来自邻居的自变量的影响，
	+ $δ$为系数向量，犯罪率不仅依赖于本区域的警力，还依赖于相邻区域的警力。
+ 可以与空间自回归模型合并为$y = λWy + Xβ + WXδ + ε$.

## 29.6 空间误差模型(SEM Spatial Errors Models)
+ $y = Xβ + u，u= ρMu +ε$，误差项存在空间自相关
+ 空间误差模型的stata操作：
```stata
	reg crime hoval income //利用spatdiag诊断是否存在空间效应，需要先进行OLS回归。
	spatdiag,weights(W) //一部分P值<0.05，拒绝无空间自相关的原假设。
	spatwmat using columbusswm.dta,name(W) eigenval(E) //需要首先计算权重矩阵W的特征值E。
	spatreg crime hoval income,weights(W) eigenval(E) model(lag) nolog //model(lag)表示估计空间自回归SAR模型，还可以使用robust稳健标准误选项。
	//自回归系数rho的估计值为0.055，p值很小，拒绝原假设认为存在空间自回归效应，下面估计空间误差模型SEM：
	spatreg crime hoval income,weights(W) eigenval(E) model(error) nolog //估计SEM模型
	//误差项的空间自回归系数记为λ=0.036，并且显著不为0，SEM对自变量回归系数的估计结果与OLS很接近，表示SEM模型正确，OLS模型也是一致的估计量，只不过MLE估计量更有效。
```

## 29.7 一般空间计量模型
+ 1、更一般的空间计量模型将SAR和SEM结合起来，$y = λWy+Xβ+u ；u= ρMu +ε$，SAR对应于 $ρ=0$ 的情形。SEM对应于 $λ=0$ 的情形。
+ 2、$W$ 为被解释变量 $y$ 的空间权重矩阵，$M$ 为扰动项 $u$ 的空间权重矩阵，二者可以相等；
+ 3、带空间自回归误差项的空间自回归模型，Spatial Autoregressive Model with Spatial Autoregressive Disturbance：SARAR
+ 4、SAR SEM以及SARAR统称为Cliff-Ord模型，（Cliff and Ord，1973，1981；Ord，1975）
+ 5、扰动项不服从独立的正态分布，则QMLE估计量不是一致的。Kelejian and Prucha（1999，1998，2004，2010）提出使用工具变量；
+ 6、通过GMM来估计SARAR模型，广义空间二阶段最小二乘（GS2SLS)，Arraiz（2010）证明此IV估计量在异方差情况下也是一致的，更为稳健；
+ 7、GS2SLS方法，需要有外生的解释变量，所以不适用于没有外生变量的纯SAR模型。
+ 8、SARAR模型可通过非官方的命令spreg来实现：
```stata
	which spreg //发现已安装
	spmat //定义和管理空间权重矩阵
	spreg //估计SARAR模型
	spivreg //估计含内生解释变量的SARAR模型
	cd e:\data
	use columbusswm.dta,clear
	spmat dta w1 a1-a49 //用dta格式的数据集来生成空间权重矩阵w1
	use columbusdata,clear
	spreg ml crime hoval income,id(id) dlmat(w1) elmat(w1) nolog //ml表示mle估计，id表示确定横截面变量
	//dlmat(w1)表示空间自回归方程的权重矩阵W， elmat(w1)空间误差方程Nederland空间权重矩阵M。
	//空间自回归系数λ显著，空间误差系数ρ不显著。以下用GS2SLS进行估计：
	spreg gs2sls crime hoval income,id(id) dlmat(w1) elmat(w1) het nolog //het表示使用异方差稳健标准误，默认为同方差。
	//由于空间误差系数ρ很不显著，下面估计不含空间误差的SAR模型：
	spreg gs2sls crime hoval income,id(id) dlmat(w1) het nolog	//只有自回归权重矩阵
```
## 29.8 含内生解释变量的SARAR模型
+ 1、$y = λWy+Xβ+Zδ+u ；u= ρMu +ε$，$Z$ 为内生解释变量，假设income是内生解释变量，hoval是工具变量，使用spivreg命令：
`spivreg crime  (income=hoval),id(id) dlmat(w1) elmat(w1) het nolog`

## 29.9空间面板模型
```stata
	net install spcs2xt.pkg 		//截面权重矩阵转空间权重矩阵
	help spcs2xt 				//得到很多空间面板命令
```
+ 1、类似于面板数据模型：固定效应的空间自回归模型，差分变换去掉个体效应以后，再用类似于横截面的空间自回归模型的MLE估计；
+ 2、随机效应的空间自回归模型，可先做广义离差变换，然后再进行MLE估计；
+ 3、决定使用固定效应还是随机效应模型时还是使用通常的豪斯曼检验；
+ 4、空间面板模型的MLE估计，通过非官方的xsmle命令来实现，此命令可以用来估计很一般的空间面板模型：
\[
\begin{equation}
	\begin{cases}
	y_{it} =τy_{i,t-1} +ρW_i'y_t+X_{it}'β+d_i'X_tδ+u_i+γ_t+ε_{it} \\\\
	ε_{it}=λm_i'ε_t+v_{it}
	\end{cases}
\end{equation}
\]

	+ $τ=0，λ=0$，空间杜宾模型SDM；
	+ $τ=0，λ=0且δ=0$，空间自回归模型SAR；
	+ $τ=0，δ=0$，空间自相关模型SAC，即上文的SARAR模型；
	+ $τ=ρ=0，δ=0$，空间误差模型SEM.
```stata
	xsmle y x1 x2 x3 ,wmat(name) emat(name) dmat(name) durbin(varlist) model(sdm) model(sac) model(sem) re fe robust dlag type(ind) type(time) type(both) noeffects
	//durbin(varlist)用于指定空间滞后的解释变量，默认所有解释变量；
	//model()指定SDM SEC SAC模型类型；
	//re,fe 表示随机效应和固定效应，默认是随机效应；
	//dlag表示加入被解释变量一阶滞后作为解释变量，即动态面板，默认为静态面板(τ=0)；
	//type(ind)指定个体效应;type(time)指定时间效应，type(both)表示既有个体效应和时间效应；
	//noeffects表示不显示直接效应、间接效应与总效应。 默认是不显示效应。
```
+ 实例：
```stata
	cd e:/data/
	use product.dta,clear
	gen lngsp = log(gsp)
	gen lnpcap = log(pcap)
	gen lnpc = log(pc)
	gen lnemp = log(emp)
	spmat use usaww using usaww.spmat
	xsmle lngsp lnpcap lnpc lnemp unemp ,wmat(usaww) model(sdm) robust effects nolog //effects显示间接效应等。
	//空间自回归系数ρ显著不为0，但是变量lnpcap,lnpc和unemp的空间滞后项不显著，使用durbin(lnemp)只对lnemp进行空间滞后。
	xsmle lngsp lnpcap lnpc lnemp unemp ,wmat(usaww) model(sdm) durbin(lnemp) robust noeffects nolog
	//durbin(lnemp)只对解释变量lnemp进行空间滞后，下面进行fe估计：
	xsmle lngsp lnpcap lnpc lnemp unemp ,wmat(usaww) model(sdm) durbin(lnemp) robust noeffects nolog fe //默认的是re
	//究竟是使用fe还是re，进行hausman检验，需要存储未使用稳健标准误的结果：
	qui xsmle lngsp lnpcap lnpc lnemp unemp ,wmat(usaww) model(sdm) durbin(lnemp) noeffects nolog fe
	est sto fe
	qui xsmle lngsp lnpcap lnpc lnemp unemp ,wmat(usaww) model(sdm) durbin(lnemp) noeffects nolog
	est sto re
	hausman fe re //hausman检验为负数，表示接受原假设，使用随机效应模型
	//输出fe re结果，进行对比
	reg2docx fe re using e:\temp\spanel.docx,replace ///
	  scalars(N r2_a(%9.3f) r2_p(%9.2f)) t(%7.2f) title("IV results") ///
	  mtitles("fe" "re") note("(* 0.1 ** 0.05 *** 0.01)")
```
## stata 15.0空间计量新增

### 数据格式
+ Three types of Sp data：
	+ data with shapefiles,
	+ data without shapefiles but including location information, or
	+ data without shapefiles or location information.

+ ID variables for crosssectional data 截面数据
```stata
	sort area_name
	generate id = _n //生成个体ID
```

+ ID variables for panel data 面板数据
```stata
	egen area_id = group(area_name)
```

### 案例：从shapefile创建stata格式,以美国为例：
+ 处理shapefile数据：

```stata
    cd E:\data\shapefile\
    cdout
    *step 2: Translate the shapefile
    unzipfile tl_2016_us_county.zip //解压缩
    spshape2dta tl_2016_us_county   //生成dta格式数据"tl_2016_us_county_shp.dta"和"tl_2016_us_county.dta"
    	                              //spshape2dta将地图文件中的shp和dbf格式的文件，转换为.dta和_shp.dta两个数据集，二者是关联的。
    *step 3: Look at the data
    use tl_2016_us_county_shp.dta,clear
    browse 	    //_shp.dt数据每个county的经纬度数据
    use tl_2016_us_county.dta, clear //二者原本是有关联的，_ID是是关联变量，共3233个counties，但不是要研究的数据表示法；
    br                               //需要按照研究的样本个体ID生成一个关联变量fips，以便于合并。
    *step 4: Create standard ID variable
		drop fips
    generate long fips = real(STATEFP + COUNTYFP) //real函数转化为数字或缺失值，生成fips变量；
    bysort fips: assert _N==1                     //assert验证是否满足某些条件，按fips排序，确保每个fips对应一个obs
    assert fips != .                              //确保fips变量不存在缺失值
    *step 5: Tell Sp to use standard ID variable
    spset fips, modify replace                    //用fips做个体识别变量生成新的_ID变量，重新关联tl_2016_us_county.dta和tl_2016_us_county_shp.dta
    *step 6: Set coordinate units
    spset, modify coordsys(latlong, miles)        //需要根据经纬度换算为miles或者kilometers
    save,replace                                  //保存数据集tl_2016_us_county.dta
```

+ 把要研究的数据和上面生成的shapefile stata格式的数据合并起来：

```stata
//  截面数据：
    copy http://www.stata-press.com/data/r15/texas_ue.dta texas_ue.dta,replace
    use texas_ue.dta,clear
		br
    merge 1:1 fips using tl_2016_us_county.dta    //截面数据的合并1：1，上面处理的tl_2016_us_county.dta和研究数据texas_ue.dta合并起来；
                                                  //实际上，相当于将tl_2016_us_county.dta中代表位置的变量和_ID、_CX和_CY合并到研究数据中。
    keep if _merge==3
    drop _merge
    rename NAME countyname
    drop STATEFP COUNTYFP COUNTYNS GEOID
    drop NAMELSAD LSAD CLASSFP MTFCC CSAFP
    drop CBSAFP METDIVFP FUNCSTAT
    drop ALAND AWATER INTPTLAT INTPTLON
    save texas_ue_map.dta,replace                 //保存合并入位置信息的研究数据为texas_ue_map.dta
    *另外一个截面数据的案例：
    copy http://www.stata-press.com/data/r15/project_cs.dta project_cs.dta,replace
    use project_cs.dta,clear
    merge 1:1 fips using tl_2016_us_county.dta
    keep if _merge == 3
    drop _merge
    save project_cs_map.dta,replace

// 面板数据：
    copy http://www.stata-press.com/data/r15/project_panel.dta project_panel.dta,replace
    use project_panel.dta,clear
    xtset fips time
    spbalance
    merge m:1 fips using tl_2016_us_county.dta
    keep if _merge == 3
    drop _merge
    save project_panel_map.dta,replace
    spset
    xtset //依然是面板数据
```

### 空间计量的一般过程
```stata
    cd E:\data\shapefile\
    use texas_ue_map.dta,clear
    describe               //描述统计
    summarize unemployment //看一下unemployment变量的描述统计
    grmap,activate         //需要先激活grmap模式，help spmap看spmap命令
    grmap unemployment     //根据unemployment变量画地图上的热力图
    regress unemployment college //先进行OLS，结果很显著，进一步检验残差项是否存在空间自相关，the Moran test；
    spmatrix create contiguity W //根据是否共享边界生成近邻矩阵W
    estat moran,errorlag(W)      //the moran test,检验残差是否空间自相关，空间权重矩阵为W，p=0，拒绝无空间自相关的原假设，认为残差存在空间自相关
    spregress unemployment college,gs2sls //此时没有指定空间权重矩阵，虽是空间命令spregress，但结果和regress近似；
    spregress unemployment college,ml
    *加入空间滞后的因变量Wy，分别进行gs2sls和ml回归：
    spregress unemployment college,gs2sls dvarlag(W) //dvarlag(W)空间滞后的因变量，
    spregress unemployment college,ml dvarlag(W)     //gs2sls方法更稳健，速度也快; ml方法需要残差的正态性假设，可以加vce(robust)选项，但是比较耗时；
    *对此模型的解释：
    estat impact //该命令报告直接和间接效应，y的变化除了来自x，也有Wy中的影响，Wy的影响可以细分为直接和间接影响：
    //结果如下：
    progress   :100%

    Average impacts                                 Number of obs     =        254

    ------------------------------------------------------------------------------
                 |            Delta-Method
                 |      dy/dx   Std. Err.      z    P>|z|     [95% Conf. Interval]
    -------------+----------------------------------------------------------------
    direct       | //直接效应，来自于本地区x变量的影响
         college |  -.0907613   .0128641    -7.06   0.000    -.1159745   -.0655482
    -------------+----------------------------------------------------------------
    indirect     | //间接效应：来自于其它地区的y的影响
         college |  -.0368846   .0109843    -3.36   0.001    -.0584133   -.0153558
    -------------+----------------------------------------------------------------
    total        | //总效应=直接效应+间接效应
         college |  -.1276459   .0196293    -6.50   0.000    -.1661186   -.0891733
    ------------------------------------------------------------------------------
    gen unemp100  = unemployment*100             //因变量乘以100，再进行回归，观察对回归系数的影响；
    spregress unemp100 college,gs2sls dvarlag(W) //100*y进行回归和前面的回归系数对比，β1变成100倍，β2是Wy的系数不变；
    estat impact                                 //但是估计的直接和间接效应增加了100倍；

    *加入空间滞后的自变量Wx，进行gs2sls回归：
    spregress unemployment college,gs2sls ivarlag(W:college)
    estat impact //计算直接和间接效应
    //结果如下：
    progress   :100%

    Average impacts //计算的是平均效应，每个地区来自于它自己相邻的地区的间接影响不一样；

    ------------------------------------------------------------------------------
                 |            Delta-Method
                 |      dy/dx   Std. Err.      z    P>|z|     [95% Conf. Interval]
    -------------+----------------------------------------------------------------
    direct       | //直接效应，来自于本地区的x的直接影响
         college |   -.077997   .0138127    -5.65   0.000    -.1050695   -.0509245
    -------------+----------------------------------------------------------------
    indirect     | //间接效应，来自于它相邻地区的x的影响
         college |  -.0715273   .0166314    -4.30   0.000    -.1041243   -.0389303
    -------------+----------------------------------------------------------------
    total        | //总效应
         college |  -.1495243   .0170417    -8.77   0.000    -.1829255   -.1161231
    ------------------------------------------------------------------------------

    *扰动项存在空间自回归的情形，SEM:εi = ρWεj +u
    spregress unemployment college,gs2sls errorlag(W) //误差空间自回归系数ρ=0.77，并且显著
    estat impact //误差空间自相关没有产生间接效应，简介效应为0

    *同时考虑空间滞后因变量、自变量和扰动项空间自相关三种情况,并且有不同的空间权重矩阵W和V的模型：
    spregress y x1 x2,gs2sls dvarlag(W) ivarlag(W:x1 x2) ivarlag(V:x1) errorlag(W)


***空间计量的命令***
    spregress,gs2sls
    spregress,ml
    spivregress
    spxtregress
    spxtregress,re
    spxtregress,fe
    **一般空间命令：
    spregress y x1 x2,gs2sls dvarlag(W) ivarlag(W:x1 x2) ivarlag(V:x1) errorlag(W)
    estat impact //给出平均效应
                 //gs2sls假定误差项是iid，但不必是正态分布的，
                 //heteroskedastic选项，可以放松假定，存在异方差的情况。
    spregress y x1 x2,ml dvarlag(W) ivarlag(W:x1 x2) ivarlag(V:x1) errorlag(W)
                 //ml需要误差项是独立同分布且是正态分布，比较耗时，但是更有效。
                 //如果放松正态性的假定，可以使用vce(robust)选项。
    spivregress y x1 x2 (z = x3),dvarlag(W) ivarlag(W:x1 x2) ivarlag(V:x1) errorlag(W)
                 //也可以使用heteroskedastic选项，处理异方差的情况。
    **工具变量空间回归案例：
        cd E:\data\shapefile\DUI
        copy http://www.stata-press.com/data/r15/dui_southern.dta dui_southern.dta //下载慢或出错，开代理直接复制到浏览器下载
        copy http://www.stata-press.com/data/r15/dui_southern_shp.dta dui_southern_shp.dta
        use dui_southern.dta,clear
        spset
        spmatrix create contiguity W
        spmatrix create idistance M
        spmatrix dir                 //查看当前权重矩阵
        spivregress dui nondui vehicles i.dry (police = elect), dvarlag(W) errorlag(W)
        estat impact
        spivregress dui nondui vehicles i.dry (police = elect), dvarlag(W) errorlag(W) ivarlag(W: i.dry)
        estat impact
        spivregress dui nondui vehicles i.dry (police = elect), dvarlag(W) errorlag(W) ivarlag(W: i.dry) dvarlag(M) errorlag(M) ivarlag(M: i.dry)
    **面板数据空间回归，案例在最后：
    spxtregress y x1 x2,re dvarlag(W) errorlag(W) ivarlag(M:x1 x2)
    spxtregress y x1 x2,re sarpanel dvarlag(W) errorlag(W) ivarlag(M:x1 x2)
                 //sarpanel
    spxtregress y x1 x2,fe dvarlag(W) errorlag(W) ivarlag(M:x1 x2)

    regress y x1 x2
    estat moran,error(W) error(M)
                 //莫兰检验，随机误差项是否存在空间自相关，原假设：无空间自相关。

***莫兰检验案例：
    cd e:\data //先复制案例数据
    copy http://www.stata-press.com/data/r15/homicide1990.dta homicide1990.dta
    copy http://www.stata-press.com/data/r15/homicide1990_shp.dta homicide1990_shp.dta
    use homicide1990.dta,clear
    spset     //定义空间数据，看是否关联_shp.dta数据，结果如下：
        Sp dataset homicide1990.dta
                    data:  cross sectional      //截面数据
         spatial-unit id:  _ID                  //关联变量_ID
             coordinates:  _CX, _CY (planar)    //坐标信息
        linked shapefile:  homicide1990_shp.dta //和_shp.dta关联
    grmap hrate
    spmatrix create contiguity W                //创建空间权重矩阵，共有边界定义的W
    regress hrate                               //只对y进行回归
    estat moran,errorlag(W)
    spmatrix create idistance M                  //创建距离倒数的空间权重矩阵M
    estat moran,errorlag(W) errorlag(M)          //联合检验任意权重矩阵下是否存在空间自相关
    reg hrate ln_population ln_pdensity gini     //一般线性回归后，莫兰检验
    estat moran,errorlag(W)                      //拒绝原假设，存在空间自相关，估计SEM
    spregress hrate ln_population ln_pdensity gini,gs2sls errorlag(W)
        //spregress不能简写

***检验数据是否空间平衡面板 spbalance
    xtset ID time //spbalance之前首先使用xtset命令
    spbalance     //是否平衡
    spbalance,balance //drop obs使得平衡，此时drop掉的是按时间time判断的，如果有个体不存在某个年份，则这个年份的obs全部drop
    *但是一些个体分别存在一些间断的time，则最后所有time的obs都drop了，解决办法：
        cd E:\data\shapefile\america
        spshape2dta County_2010Census_DP1
        use County_2010Census_DP1.dta, clear
        copy http://www.stata-press.com/data/r15/cbp05_14co.dta cbp05_14co.dta
        use cbp05_14co, clear
        merge m:1 GEOID10 using County_2010Census_DP1
        keep if _merge ==3
        drop _merge
        save cbp05_14co_map.dta,replace
        xtset _ID year    //先设置面板数据
        spbalance         //提示数据not strongly balanced
        spbalance,balance //强制平衡，删除了2005-2009年间的所有obs，原因是只有一个个体，只有5年的数据
        use cbp05_14co_map.dta,clear
        order _ID year
        bysort _ID:gen npanel = _N //记住此命令，根据_ID,产生一个新变量npanel等于每个个体的obs
        order npanel
        tabulate npanel            //统计npanel的情况，都等于10，只有5个等于5的，找出它
        list _ID state NAMELSAD10 year npanel if npanel!=10 //将个体年数不等于5的个体显示出来，只有一个县是5年，删除它
        drop if _ID == 400 //删除了只有5年的个体，面板数据就平衡了
        xtset _ID year
        spbalance
        disp `r(balanced)' //r(balanced)返回值为1，则数据是平衡的，否则非平衡

***生成感兴趣的子样本，保存数据后使用spcompress命令，则会对应生成响应的_shp.dta文件，而原来的dta数据不变
    cd E:\data\shapefile
    use tl_2016_us_county.dta,clear
    keep if STATEFP =="48" //生成STATEFP=48的样本子集,共254个obs
    save texas.dta,replace //保存数据，后使用spcompress
    spcompress //texas.dta保存，并生成响应的texas_shp.dta文件，而原来的tl_2016_us_county.dta和tl_2016_us_county_shp.dta数据不变；

***计算两个_ID之间的平面距离
    spdistance 48021 48041 //参数为两个ID的编号，以平面坐标系统计算的
    spset,modify coordsys(latlong,kilometers) //sp假设是平面坐标系统，转换为经纬度系统，并以km表示距离





***按province34.dta地图格式整理中国2007年34个地区的GDP数据
    cd E:\data\shapefile\china\province34
    copy "E:\学术论文\C论文\Data\govern expenditures\zongshuju.dta" zongshuju.dta,replace
    use zongshuju.dta,clear
    egen area_id = group(area)
    order area_id          //需要修改各地区对应的数字编码,修改为excel按首字母排序，因为要合并的地图文件时按首字母排序的。
    sort area_id year
    rename area_id _ID
    keep _ID area year gdp //全部删除，只保留gdp一个指标，用来画图，
    keep if year ==2007    //只保留2007年的横截面数据，然后手动添加港澳台地区
    save huizong.dta,replace
    use huizong.dta,replace //34个按首字母排序的省份gdp数据已准备好

***整理包含港澳台地区的34个区域的地图 province34.dta/province34_shp.dta
    cd E:\data\shapefile\china\province34
    spshape2dta 省级行政区,saving(province34)
    use province34.dta,clear                   //大致浏览一下数据,34个地区，包含港澳台，但是有乱码，需要转码
    clear                                    //转码前先clear
    unicode encoding set gb18030
    unicode translate province34.dta,transutf8 // 直接对源文件转码操作
    unicode erasebackups,badidea
    use province34.dta,clear //对省重新编码
        //统一区域变量area，各省名称统一
    rename NAME area
    keep _ID _CX _CY Z120401 DZM area //删除其它无关变量，然后修改名称为全称
    replace area = area+"省"     //所有地区加上“省”字
    replace area= "北京市" if area == "北京省"
    replace area= "天津市" if area == "天津省"
    replace area= "上海市" if area == "上海省"
    replace area= "重庆市" if area == "重庆省"
    replace area= "内蒙古自治区" if area == "内蒙古省"
    replace area= "宁夏回族自治区" if area == "宁夏省"
    replace area= "广西壮族自治区" if area == "广西省"
    replace area= "新疆维吾尔自治区" if area == "新疆省"
    replace area= "西藏自治区" if area == "西藏省"
    replace area= "香港" if area == "香港省"
    replace area= "澳门" if area == "澳门省"
    replace area= "台湾" if area == "台湾省"
    gen fips =_n                //然后手动直接修改fips为自己已有的研究数据，顺序
    bysort fips: assert _N==1
    spset fips,modify replace //按照fips关联，生成新的_ID变量
    rename area name          //与研究数据中的area变量重名，合并时出错，所以修改为name
    rename DZM code
    save province34.dta,replace
    use province34.dta,replace
    spset

***合并地图文件到截面数据***
    use huizong.dta,replace
    merge 1:1 _ID using province34.dta  //用对应的_ID变量和地图文件合并
    keep if _merge==3
    drop _merge              //合并以后的文件依然和province34_shp.dta保持关联
    save huatu34.dta         //先保存为画图专用，此处不要加replace，后面生成的子文件才会关联
    spcompress               //为huatu34生成对应的huatu34_shp.dta文件，而不修改原来的province34.dta和province34_shp.dta文件。
                             //此时生成了hautu34.dta和huatu34_shp.dta文件，可以为以后作图专用。
    spset                    //可以看到hautu34.dta和huatu34_shp.dta文件已关联，内存中暂时关联，再次打开和源文件关联
    clear
    use huatu34.dta,clear    //再次打开已经和huatu34_shp.dta失去关联，只是生成的瞬间在内存中时关联的
    spset                    //再次打开整理后spcompress生成的hautu34.dta数据，看是否和huatu34_shp.dta关联
    grmap gdp                //尝试画图看是否存在问题
        //复制到pic34文件夹,重新建立关联
    cd E:\data\shapefile\china\pic34
    use huatu34.dta,clear
    spset,modify shpfile(huatu34_shp) //二者建立关联,关联后要保存！！！
    save huatu34.dta,replace
    grmap gdp                         //已关联成功


***案例：空间面板
    cd e:\data
    copy http://www.stata-press.com/data/r15/homicide_1960_1990.dta homicide_1960_1990.dta //数据复制到本地当前文件夹
    copy http://www.stata-press.com/data/r15/homicide_1960_1990_shp.dta homicide_1960_1990_shp.dta
    use homicide_1960_1990,clear
    use homicide_1960_1990_shp.dta,clear    //确定数据都下载到文件夹下面了
    xtset _ID year                          //先定义面板数据
    spset                                   //再定义空间数据，看同路径下是否关联_shp.dta的文件
**随机效应：
    *先用一般面板xtreg命令建立随机效应模型：
    xtreg hrate ln_population ln_pdensity gini i.year,re //非空间命令回归结果为后边空间结果做对比
    *先生成相邻权重矩阵
    spmatrix create contiguity W if year ==1990 //去面板数据的任一年生成相邻权重矩阵，默认已标准化
    *因变量空间滞后、扰动项空间自回归模型：
    spxtregress hrate ln_population ln_pdensity gini i.year,re dvarlag(W) errorlag(W)
    *估计gini变量的直接和间接效应
    estat impact gini
    *生成距离倒数矩阵M
    spmatrix create idistance M if year ==1990
    spmatrix dir //生成的权重矩阵都保存在内存中，此命令可显示内存中所有的已生成的权重矩阵；
    *看gini的影响是否随时间发生变化，加入与时间虚拟变量的交互项：
    spxtregress hrate ln_population ln_pdensity c.gini#i.year,re dvarlag(M) error(M)
    spxtregress hrate ln_population ln_pdensity c.gini##i.year,re dvarlag(M) error(M)
        //两个##号表示，gini和i.year,以及他们的交互项都加入了回归。
-------------------------------------------------------------------------------
        hrate |      Coef.   Std. Err.      z    P>|z|     [95% Conf. Interval]
--------------+----------------------------------------------------------------
hrate         |
ln_population |   .7908003   .1764818     4.48   0.000     .4449023    1.136698
  ln_pdensity |  -.1223671    .166526    -0.73   0.462     -.448752    .2040178
         gini |   17.82039   4.278774     4.16   0.000     9.434147    26.20663
              | //两个#表示：gini、 i.year和gini#i.year的系数都估计了
         year |
        1970  |  -2.456656   2.303066    -1.07   0.286    -6.970582     2.05727
        1980  |  -9.470622   2.501527    -3.79   0.000    -14.37352    -4.56772
        1990  |  -22.81817   2.528689    -9.02   0.000    -27.77431   -17.86203
              |
  year#c.gini |
        1970  |   6.664314   6.130435     1.09   0.277    -5.351118    18.67975
        1980  |   24.86122   6.715024     3.70   0.000     11.70002    38.02242
        1990  |   57.40946   6.691097     8.58   0.000     44.29515    70.52376
              |
        _cons |  -11.17804   2.061042    -5.42   0.000    -15.21761   -7.138476
--------------+----------------------------------------------------------------
M             |
        hrate |    .694492   .0496075    14.00   0.000     .5972631    .7917209
      e.hrate |   1.950078   .0513563    37.97   0.000     1.849421    2.050735
--------------+----------------------------------------------------------------
     /sigma_u |   2.696022   .1147302                      2.480277    2.930533
     /sigma_e |   5.645628   .0618616                      5.525674    5.768186
-------------------------------------------------------------------------------
Wald test of spatial terms:          chi2(2) = 1711.10    Prob > chi2 = 0.0000
    //wald检验：拒绝原假设，则认为空间滞后项系数显著不为0。

    *检验交互项是否显著：
    contrasts c.gini#year
    *计算各年份的gini的直接和间接效应：
    estat impact gini if year ==1960
    estat impact gini if year ==1960
    estat impact gini if year ==1960
**带sarpanel选项的随机效应模型：
    spxtregress hrate ln_population ln_pdensity c.gini##i.year,re sarpanel dvarlag(M) error(M)
**固定效应：
    *先估计无空间效应的一般固定效应模型：
    xtreg hrate ln_population ln_pdensity gini,fe
    *带空间滞后因变量的固定效应模型：
    spxtregress hrate ln_population ln_pdensity gini,fe dvarlag(M)
    *fe不能加入i.year变量，因为固定效应每个个体的i.year不同，但是可以加入交互项
    spxtregress hrate ln_population ln_pdensity c.gini#i.year,fe dvarlag(M) errorlag(M)
    *计算直接和间接效应
    estat impact

export excel using "gdp.xlsx",replace firstrow(variables)
```
